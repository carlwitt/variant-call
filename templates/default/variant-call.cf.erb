// untar
deftask untar( <out( File )> : tar( File ) )in bash *{
  tar xf $tar
  out=`tar tf $tar`
}*

// gunzip
deftask gunzip( out( File ) : gz( File ) )in bash *{
  gzip -c -d $gz > $out
}*

// fastqc
deftask fastqc( zip( File ) : fastq( File ) )in bash *{
    fastqc -f fastq --noextract -o ./ $fastq
    zip=`ls *.zip`
}*

// bowtie2
deftask bowtie2-build( idx( File ) : fa( File ) )
in bash *{
  bowtie2-build $fa bt2idx
  tar cf $idx --remove-files bt2idx.*
}*

deftask bowtie2-align(
  bam( File )
: idx( File ) [fastq1( File ) fastq2( File )] )in bash *{

  tar xf $idx
  bowtie2 -D 5 -R 1 -N 0 -L 22 -i S,0,2.50 --no-unal -x bt2idx \
  -1 $fastq1 -2 $fastq2 -S - | samtools view -b - > $bam
  rm bt2idx.*
}*

deftask samtools-faidx( fai( File ) : fa( File ) )in bash *{
  samtools faidx $fa
  fai=$fa.fai
}*

deftask samtools-sort( sortedbam( File ) : bam( File ) )in bash *{
  sortedbam=alignment-sorted.bam
  samtools sort -m 1G $bam alignment-sorted
}*

deftask samtools-mpileup( mpileup( File ) : bam( File ) [fa( File ) fai( File )] )in bash *{
  ln -sf $fai $fa.fai
  samtools mpileup -f $fa $bam > $mpileup
}*

deftask samtools-merge( merged( File ) : <bam( File )> )in bash *{
  if [ ${#bam[@]} -eq "0" ]
  then
    echo "No files to merge." >&2
    exit -1
  else
    if [ ${#bam[@]} -eq "1" ]
    then
      merged=$bam
    else
      samtools merge -f $merged ${bam[@]}
    fi
  fi
}*

// varscan
deftask varscan( vcf( File ) : mpileup( File ) )in bash *{
  varscan mpileup2snp $mpileup --output-vcf --p-value 99e-02 > $vcf
}*

// annovar
deftask annovar( fun( File ) exonicfun( File ) : <vcf( File )> db buildver )in bash *{
  fun=table.variant_function
  exonicfun=table.exonic_variant_function
  cat ${vcf[@]} | \
  convert2annovar.pl -format vcf4 - | \
  annotate_variation.pl -buildver $buildver -geneanno -outfile table - $db
}*

deftask per-chromosome(
    fun( File ) exonicfun( File )
  : [fa( File ) fai( File ) idx( File )] <fastq1( File )> <fastq2( File )> db ) {

  sortedbam = per-fastq(
    fa:     fa,
    idx:    idx
    fastq1: fastq1,
    fastq2: fastq2 );

  mergedbam = samtools-merge( bam: sortedbam );

  mpileup = samtools-mpileup(
    bam: mergedbam
    fa:  fa
    fai: fai );

  vcf = varscan( mpileup: mpileup );

  fun exonicfun = annovar(
    vcf:      vcf
    db:       db
    buildver: 'hg38' );

}

deftask per-fastq(
    sortedbam
  : idx( File ) [fastq1( File ) fastq2( File )] ) {

  bam = bowtie2-align(
    idx:    idx,
    fastq1: fastq1,
    fastq2: fastq2 );

  sortedbam = samtools-sort( bam: bam );
}




// input files
hg38-tar = 'hg38/hg38.tar';

fastq1-gz = // 'kgenomes/SRR359188_1.filt.fastq.gz'
            // 'kgenomes/SRR359195_1.filt.fastq.gz'
            // 'kgenomes/SRR359198_1.filt.fastq.gz'
            'kgenomes/SRR359201_1.filt.fastq.gz';

fastq2-gz = // 'kgenomes/SRR359188_2.filt.fastq.gz'
            // 'kgenomes/SRR359195_2.filt.fastq.gz'
            // 'kgenomes/SRR359198_2.filt.fastq.gz'
            'kgenomes/SRR359201_2.filt.fastq.gz';

db = 'annodb_hg38';

// workflow definition
// fa = untar( tar: hg38-tar );
fa = gunzip( gz: 'hg38/chr22.fa.gz' );

fastq1 = gunzip( gz: fastq1-gz );
fastq2 = gunzip( gz: fastq2-gz );

qc = fastqc( fastq: fastq1 fastq2 );

bt2idx = bowtie2-build( fa: fa );
fai = samtools-faidx( fa: fa );

fun exonicfun = per-chromosome(
  fa:     fa,
  fai:    fai,
  idx:    bt2idx,
  fastq1: fastq1,
  fastq2: fastq2,
  db:     db );


// query
fun exonicfun qc;

